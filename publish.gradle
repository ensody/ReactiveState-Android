// Based on:
// * https://gist.github.com/Robyer/a6578e60127418b380ca133a1291f017
// * https://stackoverflow.com/questions/26874498/publish-an-android-library-to-maven-with-aar-and-source-jar

apply from: "$rootDir/secrets.gradle"

version = getProperty('version')
group groupId

def bintrayUsername = secrets.getProperty("bintray.username") ?: System.getenv("BINTRAY_USERNAME")
def bintrayApiKey = secrets.getProperty("bintray.apikey") ?: System.getenv("BINTRAY_APIKEY")

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

apply plugin: 'org.jetbrains.dokka'
dokka {
    outputDirectory = "$rootDir/docs"
    outputFormat = 'gfm'
}

apply plugin: "maven-publish"
publishing {
    repositories {
        maven {
            name = "Bintray"
            url = uri("https://api.bintray.com/maven/$bintrayUserOrg/${bintrayRepo}/${group}:${project.name}")
            credentials {
                username = bintrayUsername
                password = bintrayApiKey
            }
        }
    }
}

// Because the components are created only during the afterEvaluate phase, we must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
            }
        }
    }
}

apply plugin: "com.jfrog.bintray"
bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['release']
    pkg {
        userOrg = bintrayUserOrg
        repo = bintrayRepo
        name = "$group:${project.name}"
        licenses = rootProject.licenses
        vcsUrl = rootProject.vcsUrl
        websiteUrl = rootProject.website
    }
}
