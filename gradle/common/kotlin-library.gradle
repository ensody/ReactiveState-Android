class Config {
    Boolean kotlinExplicitApiMode = true
    Boolean allWarningsAsErrors = true
    List<String> kotlinCompilerArgs = []
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

ext.kotlinLibrary = { args = [:] ->
    apply from: "$rootDir/gradle/common/kotlin-common.gradle"

    Config config = new Config(args)

    kotlinCommon()

    kotlin {
        // TODO/XXX: Workaround for freeCompilerArgs not getting respected by Android Studio for Kotlin Multiplatform
        if (config.kotlinExplicitApiMode) {
            explicitApi()
        }

        targets.all {
            compilations.all {
                kotlinOptions {
                    allWarningsAsErrors = config.allWarningsAsErrors
                    freeCompilerArgs += getCompilerArgs(
                        kotlinExplicitApiMode: config.kotlinExplicitApiMode,
                        kotlinCompilerArgs: config.kotlinCompilerArgs,
                    )
                }
            }
        }
    }
}
